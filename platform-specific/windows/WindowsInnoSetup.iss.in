; Script initially generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "SiriL"
#define MyAppExeName "siril.exe"
#define MyAppVersion @PACKAGE_VERSION@
#define MyBuildBasePath @MESON_INSTALL_PREFIX@
#define MySourceBasePath @PACKAGE_SOURCE_DIR@

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{ADA3C347-68C3-4EAA-92B3-C1BDBD836EDB}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher=Free-Astro
AppPublisherURL=https://www.siril.org/
AppSupportURL=https://www.siril.org/
AppUpdatesURL=https://www.siril.org/
DefaultDirName={commonpf}\{#MyAppName}\{#MyAppVersion}
DefaultGroupName={#MyAppName}
LicenseFile={#MyBuildBasePath}\share\doc\siril\LICENCE.md
OutputDir={#MyBuildBasePath}\..\
OutputBaseFilename={#MyAppName}_{#MyAppVersion}
Compression=lzma
SolidCompression=yes
ChangesAssociations=yes
ArchitecturesInstallIn64BitMode=x64

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"; InfoBeforeFile: "{#MySourceBasePath}\platform-specific\windows\About-EN.rtf"; InfoAfterFile: "{#MySourceBasePath}\platform-specific\windows\Scripts-EN.rtf"
Name: "fr"; MessagesFile: "compiler:Languages\French.isl"; InfoBeforeFile: "{#MySourceBasePath}\platform-specific\windows\About-FR.rtf"; InfoAfterFile: "{#MySourceBasePath}\platform-specific\windows\Scripts-FR.rtf"

[Tasks]
Name: desktopicon; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "{#MyBuildBasePath}\bin\siril.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
;Source: "C:\Users\Cyril\Pictures\SiriL_Travail\*"; DestDir: "{%USERPROFILE}\Pictures\"; Languages: fr; Flags: ignoreversion recursesubdirs createallsubdirs
;Source: "C:\Users\Cyril\Pictures\SiriL_Work\*"; DestDir: "{%USERPROFILE}\Pictures\"; Languages: en; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBuildBasePath}\bin\*"; DestDir: "{app}\bin"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBuildBasePath}\lib\*"; DestDir: "{app}\lib"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBuildBasePath}\share\*"; DestDir: "{app}\share"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBuildBasePath}\ssl\*"; DestDir: "{app}\ssl"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MySourceBasePath}\3rdparty\scripts\fr\*.ssf*"; DestDir: "{app}\scripts"; Languages: fr; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MySourceBasePath}\scripts\*.ssf"; DestDir: "{app}\scripts"; Languages: en; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MySourceBasePath}\3rdparty\scripts\en\*.ssf"; DestDir: "{app}\scripts"; Languages: en; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
Root: HKCR; Subkey: ".seq";                             ValueData: "{#MyAppName}";          Flags: uninsdeletevalue; ValueType: string;  ValueName: ""
Root: HKCR; Subkey: "{#MyAppName}";                     ValueData: "Program {#MyAppName}";  Flags: uninsdeletekey;   ValueType: string;  ValueName: ""
Root: HKCR; Subkey: "{#MyAppName}\DefaultIcon";         ValueData: "{app}\bin\{#MyAppExeName},1";               ValueType: string;  ValueName: ""
Root: HKCR; Subkey: "{#MyAppName}\shell\open\command";  ValueData: """{app}\bin\{#MyAppExeName}"" ""%1""";  ValueType: string;  ValueName: ""

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\bin\{#MyAppExeName}";
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\bin\{#MyAppExeName}"; Tasks: desktopicon; 
